/*
 *  IronJacamar, a Java EE Connector Architecture implementation
 *  Copyright 2016, Red Hat Inc, and individual contributors
 *  as indicated by the @author tags. See the copyright.txt file in the
 *  distribution for a full listing of individual contributors.
 *
 *  This is free software; you can redistribute it and/or modify it
 *  under the terms of the Eclipse Public License 1.0 as
 *  published by the Free Software Foundation.
 *
 *  This software is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the Eclipse
 *  Public License for more details.
 *
 *  You should have received a copy of the Eclipse Public License
 *  along with this software; if not, write to the Free
 *  Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 *  02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */

import org.asciidoctor.gradle.AsciidoctorTask

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            name 'jboss-nexus'
            url "http://repository.jboss.org/nexus/content/groups/public/"
        }
        jcenter()
    }
    dependencies {
        classpath libraries.asciidoctor
        classpath libraries.asciidoctorjpdf
        classpath libraries.asciidoctorjepub3
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply plugin: "java"
apply plugin: 'org.asciidoctor.convert'

asciidoctorj {
    version = '1.5.4'
}

defaultTasks 'buildDocs'

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// grouping tasks - declaration, see below for task dependency definitions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

task buildDocs {
    group 'Documentation'
    description 'Grouping task for performing all documentation building tasks'
}

// User Guide ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

task renderUserGuide(type: AsciidoctorTask, group: 'Documentation') {
    description = 'Renders the User Guides in HTML format using Asciidoctor.'
    sourceDir = file( 'src/main/asciidoc/user-guide' )
    outputDir = new File("$buildDir/asciidoc/user-guide")
    backends 'html5', 'pdf', 'epub3', 'docbook'
    separateOutputDirs true
    options logDocuments: true
    attributes  icons: 'font', experimental: true, 'source-highlighter': 'prettify', linkcss: true
}

// Developer Guide ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

task renderDeveloperGuide(type: AsciidoctorTask, group: 'Documentation') {
    description = 'Renders the Mapping Guides in HTML format using Asciidoctor.'
    sourceDir = file( 'src/main/asciidoc/developer-guide/' )
    outputDir = new File("$buildDir/developer-guide/")
    backends 'html5', 'pdf', 'epub3', 'docbook'
    options logDocuments: true
    attributes  icons: 'font', experimental: true, 'source-highlighter': 'prettify', linkcss: true
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// grouping tasks
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

buildDocs.dependsOn renderUserGuide
buildDocs.dependsOn renderDeveloperGuide
// the jDocBook plugin already adds its main task as a dependency of the buildDocs task

jar {
    manifest {
        attributes("Implementation-Title" : "IronJacamar Deployers")
    }
    baseName = "ironjacamar-doc"
    dependsOn buildDocs
    from sourceSets.main.output
}
