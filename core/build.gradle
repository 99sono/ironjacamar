/*
 * IronJacamar, a Java EE Connector Architecture implementation
 * Copyright 2015, Red Hat Inc, and individual contributors
 * as indicated by the @author tags. See the copyright.txt file in the
 * distribution for a full listing of individual contributors.
 *
 * This is free software; you can redistribute it and/or modify it
 * under the terms of the Eclipse Public License 1.0 as
 * published by the Free Software Foundation.
 *
 * This software is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the Eclipse
 * Public License for more details.
 *
 * You should have received a copy of the Eclipse Public License 
 * along with this software; if not, write to the Free
 * Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
 * 02110-1301 USA, or see the FSF site: http://www.fsf.org.
 */

configurations {
  api
}

dependencies {
  compile project(':api')
  compile project(':common')

  compile project(path: ':common', configuration: 'api')
  compile project(path: ':common', configuration: 'spi')
}


task apiJar(type: Jar) {
  manifest {
    attributes("Implementation-Title" : "IronJacamar Core API")
  }
  baseName = "ironjacamar-core-api"
  dependsOn classes
  from sourceSets.main.output
  include('org/ironjacamar/core/*.class', 'org/ironjacamar/core/api/**', 'org/ironjacamar/core/spi/**')
}

jar {
  manifest {
    attributes("Implementation-Title" : "IronJacamar Core Implementation")
  }
  baseName = "ironjacamar-core-impl"
  dependsOn classes, apiJar
  from sourceSets.main.output
  exclude('org/ironjacamar/core/*.class', 'org/ironjacamar/core/api/**', 'org/ironjacamar/core/spi/**')
}

artifacts {
  api apiJar
}

javadoc {
  source sourceSets.main.allJava
  exclude '**/*_$bundle*'
  exclude '**/*_$logger*'
  title = "IronJacamar / Core"
  options.doclet = "org.jboss.apiviz.APIviz"
  options.docletpath file(configurations.apiVizTool.asPath)
  options.bottom = "Copyright 2015 IronJacamar (<a href=\"http://www.ironjacamar.org\">http://www.ironjacamar.org</a>)"
  options.linksOffline 'http://docs.oracle.com/javase/7/docs/api/', 'http://docs.oracle.com/javase/7/docs/api/'
  doLast{
     copy {
       from rootProject.file('tools/api')
       into 'build/docs/javadoc'
     }   
  }
}
